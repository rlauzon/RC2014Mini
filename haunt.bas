10 CLEAR 1000
20 INPUT "DO YOU WANT INSTRUCTIONS? (Y/N)";ZZ$
40 IF ZZ$="N" OR ZZ$="n"THEN 60
50 GOSUB 110
60 GOSUB 350
80 GOSUB 620
90 GOSUB 840
100 GOTO 80
110 REM Instructions
120 PRINT "              HAUNTED HOUSE"
130 PRINT "            BY BARRY GASKINS"
140 PRINT "ANY KEY TO CONTINUE"
150 INPUT ZZ$
160 PRINT "           HAUNTED HOUSE"
170 PRINT "YOU ARE LOST IN THE WOODS AND ARE AT THE ENTRANCE TO A "
180 PRINT "STRANGE LOOKING HOUSE.    LOOKING FOR HELP AND FOOD YOU"
190 PRINT "DECIDE TO GO IN...  SUDDENLY THE DOOR"
200 PRINT "SLAMS SHUT. YOU HEAR AN EVIL LAUGH AND"
210 PRINT "A VOICE COMMING FROM THE VERY WALLS"
220 PRINT "    'YOU WILL NEVER LEAVE THIS "
230 PRINT "HOUSE. YOU WILL STAY FOREVER TO"
240 PRINT "WALK ITS ROOMS AND HALLS. HEAR THESE WORDS AND KNOW YOUR "
250 PRINT "     DOOM!!!!! HA HA HA HA...."
260 INPUT ZZ$
270 PRINT "LOOKING AROUND YOU SEE THAT THERE ARE"
280 PRINT "SEVERAL EXITS BUT NO WINDOWS."
290 PRINT "MAYBE YOU HAVE A CHANCE?"
300 PRINT "IF YOU TELL ME WHAT YOU WANT TO DO I WILL TELL YOU WHAT ";
310 PRINT "IS HAPPENING. AND IF YOU NEED HELP JUST TYPE HELP IN ";
320 PRINT "PLACE OF YOUR COMMAND"
330 INPUT ZZ$
340 RETURN
350 DIM S(13):DIM S$(13):L=1
360 DIM R$(13):DIM E$(13):DIM CO$(14)
370 FOR X=1 TO 13:S(X)=X:READ S$(X):NEXT
380 FOR X=1 TO 13:READ R$(X):NEXT
390 FOR X=1 TO 12:READ E$(X):NEXT
400 FOR X=1 TO 14:READ CO$(X):NEXT
410 RETURN
420 DATA ROPE,SWORD,KEY,BUCKET OF WATER,RUG,WAND,MOLDY FOOD,WINE,KEGS
430 DATA ASHES,ROCKS,LEAVES,NULL
440 DATA THE DEN,A TROPHY ROOM,THE SERVANTS QUARTERS,THE DINING ROOM
450 DATA THE KITCHEN,THE CELLAR,A STORAGE ROOM,THE WINE CELLAR
460 DATA AGING ROOM,A NARROW PASSAGE,A HUGE CAVE,A SMALL CAVE
470 DATA THE OUTSIDE WORLD
480 DATA SOUTH EAST AND WEST,WEST,NORTH,NORTH,SOUTH AND EAST
490 DATA UP AND EAST,SOUTH,WEST EAST AND NORTH,WEST
500 DATA EAST AND WEST,EAST AND WEST,WEST
510 DATA NORTH,SOUTH,EAST,WEST,UP,DOWN
520 DATA GET,DROP,USE,OFFER,DRINK,SEARCH,ATTACK WITH,INVENTORY
530 PRINT "YOU ARE IN "R$(L)
540 PRINT "VISIBLE ITEMS: ";
550 FOR X=1 TO 13
560 IF S(X)=L THEN PRINT S$(X)" ";
570 NEXT
580 PRINT
590 PRINT "YOU SEE EXITS "; E$(L)
600 IF L=6 AND NOT F2 THEN PRINT "THERE IS A GHOST IN THIS ROOM !!!"
610 IF L=10 AND NOT F4 THEN PRINT "A WALL OF FIRE BLOCKS YOUR WAY !!!"
620 IF L=11 AND NOT F5 THEN 640
630 GOTO 680
640 PRINT "THERE IS A HUGE HUNGRY GIANT IN HERE"
660 IF L=12 THEN GOSUB 680
670 RETURN
680 PRINT "THERE IS A HOLE IN THE CEILING AND YOU CAN SEE THE OPEN ";
690 PRINT "SKY ABOVE !!!"
700 RETURN
710 PRINT "LEGAL COMMANDS ARE:"
720 FOR X=1 TO 6
730	PRINT "     "CO$(X)
740 NEXT
750 FOR X=7 TO 13
760 PRINT "    "CO$(X)" SOMETHING"
770 NEXT
780 PRINT "    "CO$(14)
790 PRINT
800 PRINT "I ONLY LOOK AT THE FIRST 3 LETTERS OF";
810 PRINT "EACH WORD SO YOU ONLY NEED TO TYPE THE FIRST 3 LETTERS "
820 GOTO 2870
840 INPUT "WHAT DO YOU WANT TO DO ";DO$
850 IF DO$="UP" THEN X=5:GOTO 900
860 IF LEN(DO$)<3 THEN 710
870 FOR X=1 TO 14
880 IF LEFT$(DO$,3)=LEFT$(CO$(X),3) THEN 900
890 NEXT:GOTO 710
900 IF X<7 THEN 1000
910 IF X=14 THEN 1410
920 IF X=7 THEN 1480
930 IF X=8 THEN 1760
940 IF X=9 THEN 1910
950 IF X=11 THEN 2100
960 IF X=10 THEN 2250
970 IF X=12 THEN 2370
980 IF X=13 THEN 2510
990 GOTO 710
1000 ON L GOTO 1010,1060,1080,1100,1120,1160,1200,1220,1260,1290,1320,1360
1010 IF X=1 THEN 1460
1020 IF X=2 THEN L=3:RETURN
1030 IF X=3 THEN L=2:RETURN
1040 IF X=4 THEN L=5:RETURN
1050 GOTO 1460
1060 IF X=4 THEN L=1:RETURN
1070 GOTO 1460
1080 IF X=1 THEN L=1:RETURN
1090 GOTO 1460
1100 IF X=1 THEN L=5:RETURN
1110 GOTO 1460
1120 IF X=2 THEN L=4:RETURN
1130 IF X=3 THEN L=1:RETURN
1140 IF X=6 AND F1 THEN L=6:RETURN
1150 GOTO 1460
1160 IF X=3 AND F2 THEN L=8:RETURN
1170 IF X=3 THEN PRINT"THE GHOST BLOCKS YOUR WAY !"
1180 IF X=5 THEN L=5:RETURN
1190 GOTO 1460
1200 IF X=2 THEN L=8:RETURN
1210 GOTO 1460
1220 IF X=1 THEN L=7:RETURN
1230 IF X=3 THEN L=9:RETURN
1240 IF X=4 THEN L=6:RETURN
1250 GOTO 1460
1260 IF X=3 AND F3 THENL=10:RETURN
1270 I FX=4 THEN L=8:RETURN
1280 GOTO 1460
1290 IF X=3 AND F4 THEN L=11:RETURN
1300 IF X=4 THEN L=9:RETURN
1310 GOTO 1460
1320 IF X=3 AND F5 THEN L=12:RETURN
1330 IF X=4 THEN L=10:RETURN
1340 IF X=3 THEN PRINT "YOU WERE BURNED UP IN THE FIRE":GOTO 2680
1350 GOTO 1460
1360 IF X=4 THEN L=11:RETURN
1370 IF X=5 AND F6 THEN 1390
1380 GOTO 1460
1390 PRINT "YOU MADE IT OUT ALIVE !!!"
1400 PRINT "GOOD JOB ":END
1410 PRINT "  INVENTORY:"
1420 FOR X=1 TO 13
1430 IF S(X)=0 THEN PRINT S$(X)
1440 NEXT
1450 GOTO 2870
1460 PRINT " YOU CAN'T GO THAT WAY"
1470 GOTO 2870
1480 DO$=DO$+" "
1490 N=LEN(DO$)-1
1500 FOR X=1 TO N
1510 IF LEFT$(DO$,1)=" " THEN X=N
1520 DO$=RIGHT$(DO$,LEN(DO$)-1)
1530 NEXT
1540 IF LEN(DO$)<2 THEN 1620
1550 DO$=LEFT$(DO$,LEN(DO$)-1)
1560 IF LEN(DO$)<3 THEN 1620
1570 FOR X=1 TO 13
1580 IF LEFT$(DO$,3)=LEFT$(S$(X),3) THEN 1610
1590 NEXT
1600 GOTO 1620
1610 IF S(X)=L THEN S(X)=0:GOTO 1630
1620 PRINT "I SEE NO "DO$" HERE":GOTO 2870
1630 IF DO$="RUG" AND NOT F1 THEN 1710
1640 IF X=9 THEN 1660
1650 RETURN
1660 S(X)=L
1670 PRINT "THE KEGS ARE MUCH TOO BIG TO CARRY"
1680 GOTO 1890
1700 RETURN
1710 F1=1
1720 PRINT " THERE WAS A TRAPDOOR UNDER THE RUG !"
1730 PRINT
1740 E$(5)="DOWN "+E$(5)
1750 GOTO 2870
1760 DO$=DO$+" "
1770 N=LEN(DO$)
1780 FOR X=1 TO N
1790 IF LEFT$(DO$,1)=" " THEN X=N
1800 DO$=RIGHT$(DO$,LEN(DO$)-1)
1810 NEXT
1820 IF LEN(DO$)<2 THEN 1880
1830 DO$=LEFT$(DO$,LEN(DO$)-1)
1840 IF LEN(DO$)<3 THEN 1880
1850 FOR X=1 TO 13
1860 IF LEFT$(DO$,3)=LEFT$(S$(X),3) AND S(X)=0 THEN S(X)=L:RETURN
1870 NEXT
1880 PRINT "YOU DONT HAVE A "DO$
1890 GOTO 2870
1910 GOSUB 2760
1920 IF LEN(DO$)<3 THEN 1960
1930 IF LEFT$(DO$,3)="KEY" AND S(3)=0 AND L=9 AND FO THEN 1980
1940 IF LEFT$(DO$,3)="BUC" AND L=10 AND S(4)=0 THEN 2020
1950 IF LEFT$(DO$,3)="ROP" AND (S(1)=0 OR S(1)=L) AND L=12 THEN 2050
1960 PRINT "THAT HAD NO USEFUL EFFECT "
1970 GOTO 1890
1980 PRINT "YOU OPENED THE KEG TO REVEAL A PASSAGE!"
1990 F3=1
2000 E$(9)=E$(9)+" AND EAST"
2010 GOTO 2870
2020 PRINT "THE WATER PUT THE FIRE OUT"
2030 F4=1
2040 GOTO 2870
2050 F6=1
2060 E$(12)=E$(12)+" AND UP"
2070 PRINT "YOUR ROPE CATCHES SOMETHING OUTSIDE"
2080 GOTO 2870
2100 GOSUB 2760
2110 IF LEN(DO$)<3 THEN 2180
2120 IF LEFT$(DO$,3)="WIN" AND (S(8)=0 OR S(8)=L) THEN 2200
2140 IF (LEFT$(DO$,3)="BUC" OR LEFT$(DO$,3)="WAT") THEN 2160
2150 GOTO 2180
2160 IF (S(4)=0 OR S(4)=L) THEN 2220
2180 PRINT "I SEE NOTHING TO DRINK AROUND HERE"
2190 GOTO 1890
2200 PRINT "WHILE YOU WERE DRUNK A TROLL CAME BY AND ATE YOU "
2210 GOTO 2680
2220 PRINT "THE WATER WAS POISON"
2230 GOTO 2680
2250 IF NOT(L=6 OR L=11) THEN 2270
2260 GOTO 2280
2270 PRINT "THERE IS NOBODY HERE TO OFFER ANYTHING":GOTO 2870
2280 IF L=6 THEN PRINT "THE GHOST DOESN'T WANT IT":GOTO 2870
2290 GOSUB 2760
2300 IF LEFT$(DO$,3)="WIN" AND S(8)=0 THEN 2330
2310 PRINT "THE GIANT GETS MAD AND THROWS A ROCK AT YOU. "
2320 GOTO 2680
2330 PRINT "THE GIANT DRINKS THE WINE AND FALLS OVER ON THE FLOOR DRUNK"
2340 F5=1
2350 GOTO 2870
2370 GOSUB 2760
2380 IF LEN(DO$)<3 THEN 2410
2390 IF LEFT$(DO$,3)="RUG" AND L=5 THEN 2430
2400 IF LEFT$(DO$,3)="KEG" AND L=9 THEN 2470
2410 PRINT "THERE IS NOTHING SPECIAL ABOUT THAT"
2420 GOTO 2870
2430 PRINT "THERE WAS A TRAPDOOR UNDER THE RUG !!"
2440 E$(5)="DOWN "+E$(5)
2450 F1=1
2460 GOTO 2870
2470 PRINT "YOU FOUND A KEYHOLE IN ONE OF THE KEGS!"
2480 FO=1
2490 GOTO 2870
2510 DO$=" "+DO$:N=LEN(DO$):FOR X=N TO 1 STEP-1
2520 IF MID$(DO$,X,1)=" " THEN DO$=RIGHT$(DO$,N-X)
2530 FOR X=1 TO 13
2540 IF LEFT$(S$(X),3)=LEFT$(DO$,3)THEN 2570
2550 NEXT
2560 GOTO 2660
2570 IF LEFT$(DO$,3)="SWO" AND S(2)=0 AND L=6 THEN 2730
2580 IF LEFT$(DO$,3)="SWO" AND S(2)<>0 THEN 2660
2590 IF L=11 THEN 2610
2600 goto 2640
2610 PRINT "THE GIANT GRABBED YOU AND SQUEEZED YOU TO DEATH !!!"
2620 GOTO 2680
2640 PRINT "THERE IS NOTHING TO ATTACK"
2650 GOTO 2870
2660 PRINT "YOU DONT HAVE ONE OF THOSE "
2670 GOTO 1890
2680 PRINT "OH NO ....."
2690 PRINT "YOU DIED "
2700 INPUT "DO YOU WANT TO PLAY AGAIN?";A$
2710 IF LEFT$(A$,1)="N" THEN END
2720 GOTO 10
2730 F2=1
2740 PRINT "THE GHOST IS DEAD !!!"
2750 GOTO 2870
2760 DO$=DO$+" "
2770 N=LEN(DO$)-1
2780 FOR X=1 TO N
2790 IF LEFT$(DO$,1)=" " THEN X=N
2800 DO$=RIGHT$(DO$,LEN(DO$)-1)
2810 NEXT
2820 IF LEN(DO$)<2 THEN RETURN
2830 DO$=LEFT$(DO$,LEN(DO$)-1)
2840 IF LEN(DO$)<3 THEN RETURN
2850 RETURN
2870 PRINT "HIT RETURN TO CONTINUE":INPUT ZZ$
2880 RETURN
