10 PRINT "DAYS AND DATES"
20 PRINT "SELECT FUNCTION"
30 PRINT "A:FIND DAY OF WEEK"
40 PRINT "B:DAYS BETWEEN DATES"
50 PRINT "C:DATE DAYS HENCE/AGO"
60 PRINT "X:EXIT"
70 INPUT A$
80 IF A$="A" THEN 130
90 IF A$="B" THEN 140
100 IF A$="C" THEN 170
110 IF A$="X" THEN 390
120 GOTO 20
130 PRINT "ENTER";: GOSUB 200: GOSUB 320: GOSUB 370: PRINT DW$: GOTO 20
140 PRINT "FIRST";: GOSUB 200: GOSUB 320:A=J
150 PRINT "SECOND";: GOSUB 200: GOSUB 320
160 PRINT ABS (A-J);" DAYS BETWEEN DATES": GOTO 20
170 PRINT "REF";: GOSUB 200: GOSUB 320:A=J
180 INPUT "DAYS HENCE (+)/AGO(-)?";B
190 J=A+B: GOSUB 340: PRINT M;"/";D;"/";Y;" ";: GOSUB 370
191 PRINT DW$:GOTO 20
200 INPUT " DATE? (MM/DD/YYYY)";D$
210 IF LEN(D$)<8 OR LEN(D$)>10 THEN 290
220 M=VAL(LEFT$(D$,2)):E=2: GOSUB 300:D=VAL(MID$ (D$,E+1,2))
221 Y=VAL(RIGHT$ (D$,4))
230 IF D<1 OR M<1 OR M>12 OR Y<1900 OR Y>2100 THEN 290
240 IF (M=4 OR M=6 OR M=9 OR M=11) AND D>30 THEN 290
250 IF M<>2 AND D>31 THEN 290
260 IF M=2 AND D>(28+(INT (Y/4)=(Y/4))) THEN 290
270 IF (Y=1900 AND M<3) OR (Y=2100 AND M>2) THEN 290
280 RETURN 
290 PRINT "INVALID ENTRY": GOTO 200
300 IF MID$ (D$,E,1)<>"/" THEN E=E+1: GOTO 300
310 RETURN 
320 IF M<3 THEN M=M+12:Y=Y-1
330 M=M+1:J=INT (365.25*Y)+INT (30.6001*M)+D+1720982: RETURN
340 R=J+68569:P=INT (4*R/146097):R=-INT ((146097*P+3)/4)+R
350 Y=INT (4000*(R+1)/1461001):R=-INT (1461*Y/4)+31+R:M=INT (80*R/2447)
360 D=-INT (2447*M/80)+R:R=INT (M/11):M=-12*R+2+M:Y=100*(P-49)+Y+R
361 RETURN 
370 E=J+1-7*INT ((J+1)/7)
380 IF E=1 THEN DW$="SUNDAY":RETURN
381 IF E=2 THEN DW$="MONDAY":RETURN
382 IF E=3 THEN DW$="TUESDAY":RETURN
383 IF E=4 THEN DW$="WEDNESDAY":RETURN
384 IF E=5 THEN DW$="THURSDAY":RETURN
385 IF E=6 THEN DW$="FRIDAY":RETURN
386 IF E=7 THEN DW$="SATURDAY":RETURN
390 END
