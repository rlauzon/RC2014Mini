10 PRINT "LOANS AND ANNUITIES"
20 PRINT "R:FIND RATE"
30 PRINT "P:FIND PAYMENT"
40 PRINT "L:FIND LOAN AMOUNT"
50 PRINT "N:FIND NUMBER OF PAYMENTS"
60 PRINT "X:EXIT PROGRAM"
70 INPUT A$
80 IF A$="R" THEN 150
90 IF A$="P" THEN 210
100 IF A$="L" THEN 230
110 IF A$="N" THEN 260
120 IF A$="X" THEN 380
130 GOTO 3030
140 REM Find Rate
150 GOSUB 300: GOSUB 310: GOSUB 320: GOSUB 340
160 B=A/P:R=1/B-B/(N*N)
170 C=1-(1+R)^-N:D=R*B-C:E=D/(C/R-(N*(1+R)^(-N-1)))
180 R=R-E: IF ABS(E)>1E-7 THEN 170
190 I=R*M*100:I=.01*INT (100*I+.5)
200 PRINT "TRUE RATE IS";I;"%": GOSUB 350: GOTO 10
210 GOSUB 300: GOSUB 330: GOSUB 320
220 P=A*R/(1-(1+R)^-N):P=.01*INT (100*P+.5)
225 PRINT "PAYMENT IS $";P: GOSUB 350: GOTO 10
230 GOSUB 310: GOSUB 330: GOSUB 320
240 A=P*(1-(1+R)^-N)/R:A=.01*INT (100*A+.5)
250 PRINT "LOAN AMOUNT IS $";A: GOSUB 350: GOTO 10
260 GOSUB 300: GOSUB 330: GOSUB 310
270 IF A*R/P>1 THEN 370
280 N=-(LOG (1-A*R/P))/(LOG (1+R))
290 PRINT INT(N+(N<>INT(N)));" PAYMENTS": GOSUB 350: GOTO 10
300 INPUT "LOAN AMOUNT? $";A: RETURN 
310 INPUT "PAYMENT? $";P: RETURN 
320 INPUT "NUMBER OF PAYMENTS? ";N: RETURN 
330 INPUT "TRUE INTEREST RATE IN %? ";I: GOSUB 340:R=I/(M*100): RETURN 
340 INPUT "PERIODS PER YEAR? ";M: RETURN 
350 S=.01*INT (100*P*N+.5):T=S+A
360 PRINT "SUM OF PAYMENTS $";S: PRINT "SUM OF INTEREST $";T: RETURN 
370 PRINT "INVALID DATA!": GOTO 10
380 END
