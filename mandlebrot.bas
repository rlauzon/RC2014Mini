10 MA=24
20 LET C$="######################################"
30 FOR Y=-39 TO 39
40 FOR X=-39 TO 39
50 CR=X/20
70 CI=Y/20
80 ZR = CR
90 ZI = CI
95 CN = 1
100 ZM = ZR*ZR
105 ZN = ZI*ZI
107 ZL = ZM+ZN
110 IF ZL>4 THEN GOTO 165
120 Z2=ZM-ZN+CR
130 ZI=ZR*ZI*2+CI
140 ZR=Z2
150 CN=CN+1
160 IF CN<MA THEN GOTO 100
165 IK=184-(7*CN)
166 GOSUB 1000
167 PA=232+CN
168 GOSUB 1050
170 PRINT MID$(C$,1+CN,1);
180 NEXT X
185 PRINT ""
190 NEXT Y
200 END
1000 REM IK Colour
1010 LET IK$=STR$(IK)
1020 PRINT CHR$(27)+"[38;5;";RIGHT$(IK$,LEN(IK$)-1);"m";
1030 RETURN
1050 REM PA Colour
1060 PA$=STR$(PA)
1070 PRINT CHR$(27)+"[48;5;"+RIGHT$(PA$,LEN(PA$)-1)+"m";
1080 RETURN
1100 REM Move Row - Col
1110 LET ROW$=STR$(ROW):LET COL$=STR$(COL)
1120 PRINT CHR$(27)+"[";RIGHT$(ROW$,LEN(ROW$)-1);";";
1130 PRINT RIGHT$(COL$,LEN(COL$)-1);"H";
1140 RETURN
2000 REM Draw Line
2010 XO$=STR$(XO):XI$=STR$(XI):YO$=STR$(YO):YI$=STR$(YI)
2020 PRINT CHR$(27)+"[?#"+RIGHT$(XO$,LEN(XO$)-1)+";";
2025 PRINT RIGHT$(XI$,LEN(XI$)-1)+";";
2030 PRINT RIGHT$(YO$,LEN(YO$)-1)+";";
2035 PRINT RIGHT$(YI$,LEN(YI$)-1);"L";
2040 RETURN
3000 REM Reset to default
3010 PRINT CHR$(27);CHR$(91);CHR$(48);CHR$(109)
3020 RETURN
